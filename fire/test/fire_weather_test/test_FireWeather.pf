module test_FireWeather
  !
  ! DESCRIPTION:
  !		Test the FATES fire weather portion of the SPITFIRE model
  !
  use FatesConstantsMod, only : r8 => fates_r8
  use SFFireWeatherMod,  only : fire_weather
  use SFNesterovMod,     only : nesterov_index
  use SFParamsMod,       only : SF_val_fdi_a, SF_val_fdi_b
  use unittestUtils,     only : endrun_msg
  use funit

  implicit none

  @TestCase
  type, extends(TestCase) :: TestFireWeather
    
    class(fire_weather), allocatable :: fireWeatherNesterov
    
    contains

      procedure :: setUp
      procedure :: tearDown 

  end type TestFireWeather

  real(r8), parameter :: tol = 1.e-13_r8

  contains 

    subroutine setUp(this)

      class(TestFireWeather), intent(inout) :: this
      
      allocate(nesterov_index :: this%fireWeatherNesterov)
      
      call this%fireWeatherNesterov%Init()
      
      SF_val_fdi_a = 17.62_r8
      SF_val_fdi_b = 243.12_r8

    end subroutine setUp

    subroutine tearDown(this)
      
      class(TestFireWeather), intent(inout) :: this
      
      if (allocated(this%fireWeatherNesterov)) deallocate(this%fireWeatherNesterov)
    
    end subroutine tearDown

    @Test 
    subroutine zero_NI_rain(this)
      ! test that over 3 mm of rain is 0.0
      class(TestFireWeather), intent(inout) :: this ! fire weather object

      call this%fireWeatherNesterov%UpdateIndex(25.0_r8, 3.1_r8, 10.0_r8, 0.0_r8)

      @assertEqual(this%fireWeatherNesterov%fire_weather_index, 0.0_r8, tolerance=tol)
      this%fireWeatherNesterov%fire_weather_index = 0.0_r8

    end subroutine zero_NI_rain

    @Test
    subroutine NI_rain_min(this)
      ! test that at 3 mm is over zero
      class(TestFireWeather), intent(inout) :: this ! fire weather object

      call this%fireWeatherNesterov%UpdateIndex(25.0_r8, 3.0_r8, 10.0_r8, 0.0_r8)

      @assertGreaterThan(this%fireWeatherNesterov%fire_weather_index, 0.0_r8, tolerance=tol)
      this%fireWeatherNesterov%fire_weather_index = 0.0_r8

    end subroutine NI_rain_min

    @Test
    subroutine NI_not_negative(this)
      ! test that NI is not negative
      class(TestFireWeather), intent(inout) :: this ! fire weather object

      call this%fireWeatherNesterov%UpdateIndex(-30.0_r8, 0.0_r8, 99.0_r8, 0.0_r8)
      @assertEqual(this%fireWeatherNesterov%fire_weather_index, 0.0_r8, tolerance=tol)
      this%fireWeatherNesterov%fire_weather_index = 0.0_r8

    end subroutine NI_not_negative

    @Test
    subroutine wind_zero(this)
      ! test that effective wind speed is zero when wind speed is zero
      class(TestFireWeather), intent(inout) :: this ! fire weather object

      call this%fireWeatherNesterov%UpdateEffectiveWindSpeed(0.0_r8, 0.5_r8, 0.5_r8, 0.0_r8)
      @assertEqual(this%fireWeatherNesterov%effective_windspeed, 0.0_r8, tolerance=tol)
  
    end subroutine wind_zero

    @Test
    subroutine wind_attenuated(this)
      ! test that effective wind speed is less than input wind speed
      class(TestFireWeather), intent(inout) :: this                 ! fire weather object
      real(r8)                              :: wind_speed = 50.0_r8 ! wind speed [m/min]

      call this%fireWeatherNesterov%UpdateEffectiveWindSpeed(wind_speed, 0.5_r8, 0.5_r8, 0.0_r8)
      @assertLessThan(this%fireWeatherNesterov%effective_windspeed, wind_speed, tolerance=tol)
  
    end subroutine wind_attenuated

    ! 'ERROR in '//trim(file)//' at line '//toString(line)

  end module test_FireWeather